with Ada.Text_IO;
with Ada.Exceptions;
with GNAT.Command_Line;
with String_Pkg;
with Ayacc_File_Names;
package body Options is

   use String_Pkg;
   use Ada.Text_IO;

   -- SCCS_ID : constant String := "@(#) options.ada, Version 1.2";
   -- Rcs_ID : constant String := "$Header: options.a,v 0.1 86/04/01 15:08:15 ada Exp $";

   Verbose_Option          : Boolean := False;
   Debug_Option            : Boolean := False;
   Interface_to_C_Option   : Boolean := False;
   Summary_Option 	       : Boolean := False;
   Loud_Option		       : Boolean := False;
   -- UMASS CODES :
   Error_Recovery_Extension_Option : Boolean := False;
   -- END OF UMASS CODES.

   Default_Stack_Size      : Natural := 8192;

   copyright : constant string :=
     "@(#) Copyright (c) 1990 Regents of the University of California.";
   copyright2 : constant string :=
     "All rights reserved.";

   procedure Put_Help_Message is
   begin
      Put_Line (Standard_Error, "ayacc version 1.1.2015");
      Put_Line (Standard_Error, copyright);
      Put_Line (Standard_Error, copyright2);
      New_Line (Standard_Error);
      Put_Line (Standard_Error, "Usage: ayacc [-cdlrsv] [-e ext] [-n size] grammar");
      Put_Line (Standard_Error, "-c          Specifies the generation of a 'C' Lex interface.");
      Put_Line (Standard_Error, "-d          Specifies the production of debugging output");
      Put_Line (Standard_Error, "-D dir      Write file to the directory specified");
      Put_Line (Standard_Error, "-l          Loud option to tell what's going on");
      Put_Line (Standard_Error, "-n size     Defines the size of the value and state stack (8192)");
      Put_Line (Standard_Error, "-r          Generate error recovery");
      Put_Line (Standard_Error, "-s          Specifies the printing of statistics about the parser");
      Put_Line (Standard_Error, "-v          Produce readable report of states generated by the parser");
      Put_Line (Standard_Error, "-e ext      Defines the extension to be used for the generated file");
      New_Line (Standard_Error);
   end Put_Help_Message;

   -- Get the program arguments and setup the options.
   procedure Get_Arguments is
      Extension : String_Type := Create(".ada");
      Directory : String_Type;
   begin
      loop
         case GNAT.Command_Line.Getopt ("c d D: l s v r: e: n:") is
            when ASCII.NUL =>
               exit;

            when 'c' =>
               Interface_to_C_Option := True;

            when 'd' =>
               Debug_Option := True;

            when 'D' =>
               Directory := Create (GNAT.Command_Line.Parameter);

            when 'l' =>
               Loud_Option := True;

            when 's' =>
               Summary_Option := True;

            when 'v' =>
               Verbose_Option := True;

            when 'r' =>
               Error_Recovery_Extension_Option := True;

            when 'e' =>
               Extension := Create (GNAT.Command_Line.Parameter);

            when 'n' =>
               begin
                  Default_Stack_Size := Natural'Value (GNAT.Command_Line.Parameter);

               exception
                  when Constraint_Error =>
                     Put_Help_Message;
                     Put_Line (Standard_Error, "Invalid size: " & GNAT.Command_Line.Parameter);
                     raise Illegal_Option;
               end;

            when others =>
               Put_Help_Message;
               raise Illegal_Option;

         end case;
      end loop;

      declare
         Name : constant String := GNAT.Command_Line.Get_Argument;
      begin
         if Name = "" then
            Put_Help_Message;
            raise Illegal_Option;
         end if;
         Ayacc_File_Names.Set_File_Names (Name, Value(Extension), Value (Directory));
      end;

   exception
      when E : GNAT.Command_Line.Invalid_Switch =>
         Put_Help_Message;
         Put_Line (Standard_Error, "Invalid option: " & Ada.Exceptions.Exception_Message (E));
         raise Illegal_Option;

   end Get_Arguments;

   function Verbose return Boolean is
   begin
      return Verbose_Option;
   end;

   function Debug return Boolean is
   begin
      return Debug_Option;
   end;

   function Interface_to_C return Boolean is
   begin
      return Interface_to_C_Option;
   end;

   function Summary return Boolean is
   begin
      return Summary_Option;
   end;

   function Loud return Boolean is
   begin
      return Loud_Option;
   end;

   -- UMASS CODES :
   function Error_Recovery_Extension return Boolean is
   begin
      return Error_Recovery_Extension_Option;
   end;
   -- END OF UMASS CODES.

   -- Returns the stack size that the parser must use.
   function Ayacc_Stack_Size return String is
   begin
      return Natural'Image (Default_Stack_Size);
   end Ayacc_Stack_Size;

end Options;
